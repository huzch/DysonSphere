#version 430
#extension GL_EXT_shader_io_blocks : enable

precision highp float;

layout(std140, binding=1) uniform ShaderParams {
    mat4 ModelView;
    mat4 ModelViewProjection;
    mat4 ProjectionMatrix;

    vec4 attractor;

    uint numParticles;
    float spriteSize;
    float damping;
    float particleScale;

    float noiseFreq;
    float noiseStrength;
};

layout( std140, binding=2 ) buffer Pos {
    vec4 pos[];
};

out gl_PerVertex {
    vec4 gl_Position;
};

out block {
     vec4 color;
     vec2 texCoord;
} Out;

void main() {
    int particleID = gl_VertexID >> 2; // 4 vertices per particle
    vec4 particlePos = pos[particleID];
    
    // Apply breathing scale to particle position
    particlePos.xyz *= particleScale;

    Out.color = vec4(0.5, 0.2, 0.1, 1.0);

    //map vertex ID to quad vertex
    vec2 quadPos = vec2( ((gl_VertexID - 1) & 2) >> 1, (gl_VertexID & 2) >> 1);

    vec4 particlePosEye = ModelView * particlePos;
    vec4 vertexPosEye = particlePosEye + vec4((quadPos*2.0-1.0)*spriteSize, 0, 0);

    Out.texCoord = quadPos;
    gl_Position = ProjectionMatrix * vertexPosEye;
}
